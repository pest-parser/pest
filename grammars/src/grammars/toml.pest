// pest. The Elegant Parser
// Copyright (c) 2018 Drago»ô Tiselice
//
// Licensed under the Apache License, Version 2.0
// <LICENSE-APACHE or http://www.apache.org/licenses/LICENSE-2.0> or the MIT
// license <LICENSE-MIT or http://opensource.org/licenses/MIT>, at your
// option. All files in the project carrying such notice may not be copied,
// modified, or distributed except according to those terms.

toml = { soi ~ (table | array_table | pair)* ~ eoi }

table       = { "[" ~ key ~ ("." ~ key)* ~ "]" ~ pair* }
array_table = { "[[" ~ key ~ ("." ~ key)* ~ "]]" ~ pair* }
pair        = { key ~ "=" ~ value }

key   = @{ identifier | string | literal }
value = _{
    inline_table |
    array |
    multi_line_string |
    string |
    multi_line_literal |
    literal |
    date_time |
    local_date_time |
    full_date |
    partial_time |
    float |
    integer |
    boolean
}

inline_table = { "{" ~ pair ~ ("," ~ pair)* ~ ","? ~ "}" | "{" ~ "}" }

array = { "[" ~ value ~ ("," ~ value)* ~ ","? ~ "]" | "[" ~ "]" }

identifier = { ('a'..'z' | 'A'..'Z' | '0'..'9' | "_" | "-")+ }

multi_line_string  = @{ "\"\"\"" ~ inner ~ "\"\"\"" }
string             = @{ "\"" ~ inner ~ "\"" }
inner              = @{ (!("\"" | "\\" | "\u{0000}" | "\u{001F}") ~ any)* ~ (escape ~ inner)? }
multi_line_literal = @{ "'''" ~ (!"'''" ~ any)* ~ "'''" }
literal            = @{ "'" ~ (!"'" ~ any)* ~ "'" }

escape  = @{ "\\" ~ ("b" | "t" | "n" | "f" | "r" | "\"" | "\\" | unicode | newline)? }
unicode = @{ "u" ~ hex ~ hex ~ hex ~ hex | "U" ~ hex ~ hex ~ hex ~ hex ~ hex ~ hex ~ hex ~ hex }
hex     =  { '0'..'9' | 'A'..'F' }

date_time       = ${ full_date ~ "T" ~ full_time }
local_date_time = ${ full_date ~ "T" ~ partial_time }

partial_time = ${ time_hour ~ ":" ~ time_minute ~ ":" ~ time_second ~ time_secfrac? }
full_date    = ${ date_fullyear ~ "-" ~ date_month ~ "-" ~ date_mday }
full_time    = ${ partial_time ~ time_offset }

date_fullyear = @{ digit ~ digit ~ digit ~ digit }
date_month    = @{ digit ~ digit }
date_mday     = @{ digit ~ digit }

time_hour    = @{ digit ~ digit }
time_minute  = @{ digit ~ digit }
time_second  = @{ digit ~ digit }
time_secfrac = @{ "." ~ digit+ }
time_offset  = ${ "Z" | ("+" | "-") ~ time_hour ~ ":" ~ time_minute }

integer = @{ ("+" | "-")? ~ int }
float   = @{ ("+" | "-")? ~ int ~ ("." ~ digits ~ exp? | exp)? }
int     = @{ "0" | ('1'..'9' ~ digits?) }
digits  = @{ (digit | ("_" ~ digit))+ }
digit   =  { '0'..'9' }
exp     = @{ ("E" | "e") ~ ("+" | "-")? ~ int }

boolean = { "true" | "false" }

newline    = _{ "\n" | "\r\n" }

WHITESPACE = _{ " " | "\t" | newline }
COMMENT    = _{ "#" ~ (!newline ~ any)* }
