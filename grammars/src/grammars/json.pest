// pest. The Elegant Parser
// Copyright (c) 2018 Drago»ô Tiselice
//
// Licensed under the Apache License, Version 2.0
// <LICENSE-APACHE or http://www.apache.org/licenses/LICENSE-2.0> or the MIT
// license <LICENSE-MIT or http://opensource.org/licenses/MIT>, at your
// option. All files in the project carrying such notice may not be copied,
// modified, or distributed except according to those terms.

json = { soi ~ (object | array) ~ eoi }

object = { "{" ~ pair ~ ("," ~ pair)* ~ "}" | "{" ~ "}" }
pair   = { string ~ ":" ~ value }

array = { "[" ~ value ~ ("," ~ value)* ~ "]" | "[" ~ "]" }

value = { string | number | object | array | bool | null }

string  = @{ "\"" ~ inner ~ "\"" }
inner   = @{ (!("\"" | "\\") ~ any)* ~ (escape ~ inner)? }
escape  = @{ "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | unicode) }
unicode = @{ "u" ~ hex{4} }
hex     = { '0'..'9' | 'a'..'f' | 'A'..'F' }

number = @{ "-"? ~ int ~ ("." ~ '0'..'9'+ ~ exp? | exp)? }
int    = @{ "0" | '1'..'9' ~ '0'..'9'* }
exp    = @{ ("E" | "e") ~ ("+" | "-")? ~ '0'..'9'+ }

bool = { "true" | "false" }

null = { "null" }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
